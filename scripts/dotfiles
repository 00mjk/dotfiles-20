#!/usr/bin/env zsh

set -e

if [[ -z $(command -v git) ]]; then
  echo "Please install git first"
  exit 1
fi

ROOTDIR=$(git rev-parse --show-toplevel)
CURRDIR=$(cd "$(dirname $0)" && pwd -P)
DOTFILES_DIR="${ROOTDIR}/dotfiles"

source "${CURRDIR}/utils.sh"

# ----------- #
# --- git --- #
# ----------- #

backup_and_symlink "${DOTFILES_DIR}/gitconfig_addons_bravoecho" "${HOME}/.gitconfig_addons_bravoecho"
git config --global include.path .gitconfig_addons_bravoecho

backup_and_symlink "${DOTFILES_DIR}/gitignore_global" "${HOME}/.gitignore_global"
git config --global core.excludesfile ~/.gitignore_global

# ------------ #
# --- tmux --- #
# ------------ #

backup_and_symlink "${DOTFILES_DIR}/tmux.conf" "${HOME}/.tmux.conf"

# ------------------- #
# --- miscellanea --- #
# ------------------- #

global_config_path=
if [[ -n "${XDG_CONFIG_HOME}" ]]; then
  global_config_path="${XDG_CONFIG_HOME}"
else
  global_config_path="${HOME}/.config"
fi

# --- global fd ignore --- #

backup_and_symlink "${DOTFILES_DIR}/fdignore" "${global_config_path}/fd/ignore"

# --- global ripgrep config --- #

backup_and_symlink "${DOTFILES_DIR}/ripgreprc" "${HOME}/.ripgreprc"
backup_and_symlink "${DOTFILES_DIR}/rgignore" "${HOME}/.rgignore"

# --- htop --- #

backup_and_symlink "${DOTFILES_DIR}/htoprc" "${global_config_path}/htop/htoprc"

# -------------- #
# --- shells --- #
# -------------- #

backup_and_symlink "${DOTFILES_DIR}/bashrc_addons_bravoecho" "${HOME}/.bashrc_addons_bravoecho"
backup_and_symlink "${DOTFILES_DIR}/zshrc_addons_bravoecho" "${HOME}/.zshrc_addons_bravoecho"
backup_and_symlink "${DOTFILES_DIR}/shell_addons_bravoecho" "${HOME}/.shell_addons_bravoecho"

if [[ "$(uname -s)" =~ Darwin ]]; then
  # prevent the "last login" message on macOS
  touch ~/.hushlogin
  backup_and_symlink "${DOTFILES_DIR}/shell_addons_mac_bravoecho" "${HOME}/.shell_addons_mac_bravoecho"
fi

function enable_shell_addon() {
  local rcfile=$1
  local addon_file=$2

  [[ -f $rcfile ]] || touch $rcfile
  append_if_missing $rcfile "[[ -e \"\${HOME}/.${addon_file}\" ]] && source ~/.${addon_file}"
}

# configuration compatible with both zsh and bash
function configure_shell_addons() {
  local shell_name=$1
  local rcfile="${HOME}/.${shell_name}rc"

  enable_shell_addon "${rcfile}" "shell_addons_bravoecho"

  if [[ "$(uname -s)" == "Darwin" ]]; then
    enable_shell_addon $rcfile "shell_addons_mac_bravoecho"
  fi

  enable_shell_addon "${rcfile}" "${shell_name}rc_addons_bravoecho"

  # Allow to further customise without the need to make changes to the provided
  # setup.
  enable_shell_addon "${rcfile}" "shell_addons_local"

  append_if_missing $rcfile "[[ -n \"\$(command -v direnv)\" ]] && eval \"\$(direnv hook ${shell_name})\" # the direnv hook must be at the end of the rc file"
}

configure_shell_addons "bash"
configure_shell_addons "zsh"
